buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.avast.gradle:docker-compose-gradle-plugin:0.5.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

group 'com.avast'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'docker-compose'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "http://downloads.mesosphere.io/maven"
    }
}

task bundle(type: Tar) {
    into("") {
        from project.configurations.runtime
        from project.tasks.jar
    }
}

dockerCompose.isRequiredBy(test)
composeUp.dependsOn(bundle)

dockerCompose {
    waitForTcpPortsTimeout = java.time.Duration.ofMinutes(1)
    stopContainers = false
}
test.doFirst {
    dockerCompose.exposeAsSystemProperties(test)
}

ext {
    scalaVersion = '2.11'
    scalaLibraryVersion = '2.11.11'
}

dependencies {
    compileOnly "org.scala-lang:scala-library:${scalaLibraryVersion}"
    compileOnly "org.slf4j:slf4j-api:1.7.10"
    compileOnly "mesosphere.marathon:plugin-interface_2.11:1.4.5"

    compile "com.bettercloud:vault-java-driver:3.0.0"

    testCompile "org.scalatest:scalatest_${scalaVersion}:2.2.5"
    testCompile "junit:junit:4.12"
    testCompile 'org.mockito:mockito-core:2.9.0'
    testCompile 'com.github.mohitsoni:marathon-client:0.4.2'
    testCompile group: 'org.asynchttpclient', name: 'async-http-client', version: '2.0.24'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
